bundle agent default_encrypt_method_sha512
{

  vars:

      "_etc_libuser_conf[defaults][crypt_style]"  string => "sha512";
      "_etc_login_defs[ENCRYPT_METHOD]"          string => "SHA512";

  files:

      # crypt_style = sha512 in [defaults] section
      "/etc/libuser.conf"
        edit_line => default:set_variable_values_ini( concat( "$(this.namespace):$(this.bundle).",
                                                              canonify( $(this.promiser) )),
                                                      "defaults"),
        if => fileexists( "/etc/libuser.conf" ),
        comment => concat(
                           "Passwords need to be protected at all times, and encryption is the",
                           " standard method for protecting passwords. If passwords are not encrypted,",
                           " they can be plainly read (i.e., clear text) and easily compromised.",
                           " Passwords that are encrypted with a weak algorithm are no more protected",
                           " than if they are kepy in plain text. ",

                           "This setting ensures user and group account administration utilities are",
                           " configured to store only encrypted representations of passwords.",
                           " Additionally, the crypt_style configuration option ensures the use of a",
                           " strong hashing algorithm that makes password cracking attacks more",
                           " difficult." );

      # ENCRYPT_METHOD SHA512
      "/etc/login.defs"
        edit_line => default:set_line_based( concat( "$(this.namespace):$(this.bundle).",
                                                     canonify( $(this.promiser) )),
                                             " ",
                                             "\s+",
                                             ".*",
                                             "\s*#\s*"),
        if => fileexists( "/etc/login.defs" ),
        comment => concat(
                           "Passwords need to be protected at all times, and encryption is the",
                           " standard method for protecting passwords. If passwords are not encrypted,",
                           " they can be plainly read (i.e., clear text) and easily compromised.",
                           " Passwords that are encrypted with a weak algorithm are no more protected",
                           " than if they are kept in plain text. ",

                           "Using a stronger hashing algorithm makes password cracking attacks more",
                           " difficult.");
}
