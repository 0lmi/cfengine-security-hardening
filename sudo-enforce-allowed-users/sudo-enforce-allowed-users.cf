bundle agent sudo_enforce_allowed_users
{
  vars:
      # Default the action policy if it's not already set
      "action_policy"
        string => "warn",
        meta => { "noreport" },
        unless => isvariable( "$(this.promiser)" );

      # Check to see if the action policy is valid or not
      "_invalid_action"
        string => ifelse( regcmp( "(warn|fix|nop)", "$(action_policy)" ), "false",
                          "true"),
        unless => isvariable( "$(this.promiser)" );

      # Default the action policy if it's invalid
      "action_policy"
        string => "warn",
        meta => { "noreport" },
        if => strcmp( "$(_invalid_action)", "true");

      "_disallowed_lines" -> { "CCE-83423-4" }
        slist => { "ALL ALL=(ALL) ALL",
                   "ALL ALL=(ALL:ALL) ALL" };

      "_check_command" string => "/usr/sbin/visudo --quiet --check --strict --file /etc/sudoers";

  files:
      "/etc/sudoers.staged"
        copy_from => default:local_dcp( "/etc/sudoers" ),
        classes => default:results( "bundle", "_init_sudoers_staged" );

      "/etc/sudoers.staged"
        edit_line => default:delete_lines_matching( @(_disallowed_lines) ),
        classes => default:results( "bundle", "_sudoers_staged" );

      "/etc/sudoers"
        copy_from => default:local_dcp( "/etc/sudoers.staged" ),
        perms => default:m( "0440" ),
        if => and( returnszero( "$(_check_command)", "noshell" ),
                   "_sudoers_staged_repaired" ),
        action => default:policy( "$(action_policy)" );

  reports:
      "$(with)"
        with => concat( "warning: Invalid action policy requested in ",
                        "'$(this.namespace):$(this.bundle)' switched to default 'warn'. ",
                        "Review setting for '$(this.namespace):$(this.bundle).action_policy'"),
        if => strcmp( "$(_invalid_action)", "true" );
}
