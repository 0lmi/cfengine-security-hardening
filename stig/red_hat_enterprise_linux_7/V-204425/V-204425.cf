body file control
{
        namespace => "rhel_7_stig";
}
bundle agent V_204425
{

      # # grep -i PermitEmptyPasswords /etc/ssh/sshd_config\\
      # PermitEmptyPasswords no
      # If no line, a commented line, or a line indicating the value "no" is returned, the required value is set.
  classes:
    rhel_7_stig:enabled::

      "$(this.bundle)_enforced"
        expression => "$(this.namespace):enforced";

      "$(this.bundle)_exception"
        expression => isvariable( "$(this.namespace):exceptions.$(this.bundle)" );

      # If there are non-commented lines that don't indicate no, OR if there is not any config that says no, it's a finding
      "$(this.bundle)_finding"
        expression => or( isgreaterthan( length( lines_without_no ), 0), # permitemptypasswords non-no
                          islessthan( length( lines_without_no ), 1) );  # permitemptypasswords no

  vars:

    rhel_7_stig:enabled::
      "config_file" string => "/etc/ssh/sshd_config";

      # Non comment lines containing PermitEmptyPasswords with a setting other than no.
      "lines_without_no" slist => grep( "^(?i)(?!#)[^\S\n]*PermitEmptyPasswords\s+(?!no\b).*$",
                                        readstringlist( $(config_file), "", "\n", inf, inf) );

      "lines_with_no" slist => grep( "^(?i)(?!#)[^\S\n]*PermitEmptyPasswords\s+(no\b).*$",
                                     readstringlist( $(config_file), "", "\n", inf, inf) );

      "finding"
        string => "V-204425",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute)" },
        if => and( "$(this.namespace):$(this.bundle)_finding",
                   not( "$(this.namespace):$(this.bundle)_exception" ) );

      "finding_with_exception"
        string => "V-204425",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute_exception)" },
        if => and( "$(this.namespace):$(this.bundle)_finding",
                   "$(this.namespace):$(this.bundle)_exception" );

  reports:
    rhel_7_stig:enabled::
      "$(this.namespace):$(this.bundle) is Enforcing without exception but automatic remediation is not available"
        if => and( "$(this.namespace):$(this.bundle)_enforced",
                   not( "$(this.namespace):$(this.bundle)_exception" ) );

    rhel_7_stig:DEBUG::
      "DEBUG $(this.namespace):$(this.bundle): Found" if => isvariable( finding );

}
