body file control
{
        namespace => "RHEL_7_STIG";
}
bundle agent V_204594
{
      # Check the version of the operating system with the following command:
      #
      # # cat /etc/redhat-release
      #
      # If the release is 7.4 or newer this requirement is Not Applicable.
      #
      # Verify the SSH daemon is configured to only use the SSHv2 protocol.
      #
      # Check that the SSH daemon is configured to only use the SSHv2 protocol with the following command:
      #
      # # grep -i protocol /etc/ssh/sshd_config
      #
      # If any protocol line other than "Protocol 2" is uncommented, this is a finding.


  classes:
    RHEL_7_STIG:enabled::
      "applicable"
        not => regline( '.*\s7\.[4-9].*', "/etc/redhat-release" );

  vars:

    RHEL_7_STIG:enabled.applicable::

      "lines"
        slist => grep("^(?!#)[^\S\n]*(?i)Protocol\s+[^2].*$",
                      readstringlist( "/etc/ssh/sshd_config", "", "\n", inf, inf) );

      "finding"
        string => "V-204594",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute)" },
        if => isgreaterthan( length( lines ), 0 );

  reports:

    RHEL_7_STIG:DEBUG::
      "DEBUG $(this.namespace):$(this.bundle): Found" if => isvariable( finding );

}
