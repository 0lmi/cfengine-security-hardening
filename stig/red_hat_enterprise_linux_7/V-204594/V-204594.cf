body file control
{
        namespace => "rhel_7_stig";
}
bundle agent V_204594
{
      # Check the version of the operating system with the following command:
      #
      # # cat /etc/redhat-release
      #
      # If the release is 7.4 or newer this requirement is Not Applicable.
      #
      # Verify the SSH daemon is configured to only use the SSHv2 protocol.
      #
      # Check that the SSH daemon is configured to only use the SSHv2 protocol with the following command:
      #
      # # grep -i protocol /etc/ssh/sshd_config
      #
      # If any protocol line other than "Protocol 2" is uncommented, this is a finding.


  classes:
    rhel_7_stig:enabled::

      "$(this.bundle)_enforced"
        expression => "$(this.namespace):enforced";

      "$(this.bundle)_exception"
        expression => isvariable( "$(this.namespace):exceptions.$(this.bundle)" );

      "$(this.bundle)_applicable"
        not => regline( '.*\s7\.[4-9].*', "/etc/redhat-release" );

      "$(this.bundle)_finding"
        expression => and( "$(this.namespace):$(this.bundle)_applicable",
                           isgreaterthan( length( lines ), 0 ) );

  vars:

    V_204594_applicable::

      "lines"
        slist => grep("^(?!#)[^\S\n]*(?i)Protocol\s+[^2].*$",
                      readstringlist( "/etc/ssh/sshd_config", "", "\n", inf, inf) );

      "finding"
        string => "V-204594",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute)" },
        if => and( "$(this.namespace):$(this.bundle)_finding",
                   not( "$(this.namespace):$(this.bundle)_exception" ) );

      "finding_with_exception"
        string => "V-204594",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute_exception)" },
        if => and( "$(this.namespace):$(this.bundle)_finding",
                   "$(this.namespace):$(this.bundle)_exception" );

  reports:
    rhel_7_stig:enabled::
      "$(this.namespace):$(this.bundle) is Enforcing without exception but automatic remediation is not available"
        if => and( "$(this.namespace):$(this.bundle)_enforced",
                   not( "$(this.namespace):$(this.bundle)_exception" ) );

    rhel_7_stig:DEBUG::
      "DEBUG $(this.namespace):$(this.bundle): Found" if => isvariable( finding );

}
