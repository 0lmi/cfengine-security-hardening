body file control
{
        namespace => "RHEL_7_STIG";
}
bundle agent V_204627
{
      # Verify that a system using SNMP is not using default community strings.
      # Check to see if the "/etc/snmp/snmpd.conf" file exists with the following command:
      # # ls -al /etc/snmp/snmpd.conf
      # -rw------- 1 root root 52640 Mar 12 11:08 snmpd.conf
      #
      # If the file does not exist, this is Not Applicable.
      #
      # If the file does exist, check for the default community strings with the following commands:
      #
      # # grep public /etc/snmp/snmpd.conf
      # # grep private /etc/snmp/snmpd.conf
      #
      # If either of these commands returns any output, this is a finding.

      # The specification does not say to ignore comment lines, but we do in order to reduce false positives.

  classes:

    RHEL_7_STIG:enabled::

      "$(this.bundle)_snmpd_config_present"
        expression => fileexists ("/etc/snmp/snmpd.conf");

    RHEL_7_STIG:V_204627_snmpd_config_present::
      "$(this.bundle)_enforced"
        expression => "$(this.namespace):enforced";

      "$(this.bundle)_exception"
        expression => isvariable( "$(this.namespace):exceptions.$(this.bundle)" );

      "$(this.bundle)_finding"
        expression => regline( "^(?!#).*(public|private).*", "/etc/snmp/snmpd.conf" );

  vars:

    RHEL_7_STIG:V_204627_snmpd_config_present::

      "finding"
        string => "V-204627",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute)" },
        if => and( "$(this.namespace):$(this.bundle)_finding",
                   not( "$(this.namespace):$(this.bundle)_exception" ) );

      "finding"
        string => "V-204627",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute_exception)" },
        if => and( "$(this.namespace):$(this.bundle)_finding",
                   "$(this.namespace):$(this.bundle)_exception" );

      "findings_detail"
        slist => grep( "^(?!#).*(public|private).*", readstringlist( "/etc/snmp/snmpd.conf", "", "\n", inf, inf ) ),
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute) detail V-204627" },
        if => regline( "^(?!#).*(public|private).*", "/etc/snmp/snmpd.conf" );

  reports:
    RHEL_7_STIG:enabled::
      "$(this.namespace):$(this.bundle) is Enforcing without exception but automatic remediation is not available"
        if => and( "$(this.namespace):$(this.bundle)_enforced",
                   not( "$(this.namespace):$(this.bundle)_exception" ) );

    RHEL_7_STIG:DEBUG::
      "DEBUG $(this.namespace):$(this.bundle): Found" if => isvariable( finding );

}
