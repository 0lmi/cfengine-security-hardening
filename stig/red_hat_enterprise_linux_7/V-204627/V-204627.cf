body file control
{
        namespace => "Red_Hat_Enterprise_Linux_7_STIG";
}
bundle agent V_204627
# https://www.stigviewer.com/stig/red_hat_enterprise_linux_7/2022-06-06/finding/V-204627
{
      # Verify that a system using SNMP is not using default community strings.
      # Check to see if the "/etc/snmp/snmpd.conf" file exists with the following command:
      # # ls -al /etc/snmp/snmpd.conf
      # -rw------- 1 root root 52640 Mar 12 11:08 snmpd.conf
      #
      # If the file does not exist, this is Not Applicable.
      #
      # If the file does exist, check for the default community strings with the following commands:
      #
      # # grep public /etc/snmp/snmpd.conf
      # # grep private /etc/snmp/snmpd.conf
      #
      # If either of these commands returns any output, this is a finding.

      # The specification does not say to ignore comment lines, but we do in order to reduce false positives.

  classes:

    Red_Hat_Enterprise_Linux_7_STIG:supported_os::

      "snmpd_config_present"
        expression => fileexists ("/etc/snmp/snmpd.conf");

  vars:

    Red_Hat_Enterprise_Linux_7_STIG:snmpd_config_present::

      "finding"
        string => "V-204627",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute)" },
        if => regline( "^(?!#).*(public|private).*", "/etc/snmp/snmpd.conf" );

      "raw_findings"
        slist => grep( "^(?!#).*(public|private).*", readstringlist( "/etc/snmp/snmpd.conf", "", "\n", inf, inf ) ),
        meta => { "inventory",
                  "attribute_name=Red Hat Enterprise Linux 7 STIG V1 findings detail V-204627" },
        if => regline( "^(?!#).*(public|private).*", "/etc/snmp/snmpd.conf" );

  reports:
    Red_Hat_Enterprise_Linux_7_STIG:DEBUG::
      "DEBUG $(this.namespace):$(this.bundle): Found" if => isvariable( finding );

}
