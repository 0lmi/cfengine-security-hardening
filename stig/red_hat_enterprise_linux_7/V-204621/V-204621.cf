body file control
{
        namespace => "RHEL_7_STIG";
}
bundle agent V_204621
{
      # If TFTP is installed and the requirement for TFTP is not documented with the ISSO, this is a finding.

      # In order to document an exception with ISSO we look for the RHEL_7_STIG.V_204621.documented_exception variable.

  vars:

    RHEL_7_STIG:enabled::
      "pkg" string => "tftp-server";

      "finding"
        string => "V-204621",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute)" },
        if => and( isgreaterthan( length( packagesmatching( "$(pkg)", ".*", ".*", ".*" ) ), 0 ),
                   not( isvariable( documented_exception )));

  reports:
    RHEL_7_STIG:DEBUG::
      "DEBUG $(this.namespace):$(this.bundle): Found" if => isvariable( finding );
      "DEBUG $(this.namespace):$(this.bundle): packages matching $(pkg): $(with)"
        with => storejson( packagesmatching( "$(pkg)", ".*", ".*", ".*" ) );

}
