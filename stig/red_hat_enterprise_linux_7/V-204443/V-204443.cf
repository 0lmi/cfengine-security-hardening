body file control
{
        namespace => "RHEL_7_STIG";
}
bundle agent V_204443
# https://www.stigviewer.com/stig/red_hat_enterprise_linux_7/2022-06-06/finding/V-204443
{
      # If ypserv is installed, it's a finding

  classes:
    RHEL_7_STIG:enabled::

      "$(this.bundle)_remediation_enabled"
        or => {
                "RHEL_7_STIG:remediation_enabled",
                some( string_replace( "$(this.bundle)", "_", "-"),
                      "$(this.namespace):config.remediation_enabled" )
        };
  vars:

    RHEL_7_STIG:enabled::
      "pkg" string => "ypserv";

      "finding"
        string => "V-204443",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute)" },
        if => isgreaterthan( length( packagesmatching( "$(pkg)", ".*", ".*", ".*" ) ), 0 );

  packages:
    RHEL_7_STIG:V_204443_remediation_enabled::
      "$(pkg)"
        policy => "absent";

  reports:
    RHEL_7_STIG:DEBUG::
      "DEBUG $(this.namespace):$(this.bundle): Found" if => isvariable( finding );
      "DEBUG $(this.namespace):$(this.bundle): packages matching $(pkg): $(with)"
        with => storejson( packagesmatching( "$(pkg)", ".*", ".*", ".*" ) );

}
