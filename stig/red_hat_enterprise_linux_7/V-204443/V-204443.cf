body file control
{
        namespace => "RHEL_7_STIG";
}
bundle agent V_204443
# https://www.stigviewer.com/stig/red_hat_enterprise_linux_7/2022-06-06/finding/V-204443
# @class `RHEL_7_STIG:V_204443_enforced` - Enables remediation.
# @class `RHEL_7_STIG:V_204443_exception` - Disables remediation (defined if the variable `RHEL_7_STIG:exceptions.V_204443` is defined ).
# @variable `RHEL_7_STIG:exceptions.V_204443` - Documents and disables remediation for this finding.
{
      # If ypserv is installed, it's a finding

  classes:
    RHEL_7_STIG:enabled::

      "$(this.bundle)_enforced"
        expression => "$(this.namespace):enforced";

      "$(this.bundle)_exception"
        expression => isvariable( "$(this.namespace).exception.$(this.bundle)" );

  vars:

    RHEL_7_STIG:enabled::
      "pkg" string => "ypserv";

      "finding"
        string => "V-204443",
        meta => { "inventory",
                  "attribute_name=$($(this.namespace):config.inventory_attribute)" },
        if => isgreaterthan( length( packagesmatching( "$(pkg)", ".*", ".*", ".*" ) ), 0 );

  packages:
    RHEL_7_STIG:V_204443_enforced.!RHEL_7_STIG:V_204443_exception::
      "$(pkg)"
        policy => "absent";

    RHEL_7_STIG:V_204443_enforced.RHEL_7_STIG:V_204443_exception::
      "$(pkg)"
        policy => "absent",
        action => default:warn_only;


  reports:
    RHEL_7_STIG:DEBUG::
      "DEBUG $(this.namespace):$(this.bundle): Found" if => isvariable( finding );
      "DEBUG $(this.namespace):$(this.bundle): packages matching $(pkg): $(with)"
        with => storejson( packagesmatching( "$(pkg)", ".*", ".*", ".*" ) );

}
