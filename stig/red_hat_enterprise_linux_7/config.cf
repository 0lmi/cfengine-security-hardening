body file control
{
        namespace => "RHEL_7_STIG";
}
bundle agent config
{
  vars:
      "inventory_attribute"
        string => "RHEL 7 STIG findings";

      "inventory_attribute_exception"
        string => "RHEL 7 STIG findings with exception";

      # This variable expects to be overridden by module input (Augments)
      # When warn, enforcement
      "mode"
        string => "warn", # (warn|enforce)
        if => not( isvariable( "$(this.namespace):$(this.bundle).mode") );

      "context"
        string => "centos_7|redhat_7",
        if => not( isvariable( "$(this.namespace):$(this.bundle).context") );
}
bundle agent run
{
  classes:

      "enabled"
        expression => "$($(this.namespace):config.context)",
        scope => "namespace",
        meta => { "report" }; # So that compliance conditions can be targeted.

      # If RHEL_7_STIG.config.mode is set to enforce we define the class to enable enforcement for all findings
      "enforced"
        expression => regcmp( "(?i)enforce", "$($(this.namespace):config.mode)");

  vars:
    RHEL_7_STIG:enabled::
      # Find the inventory bundles
      "bundles"
        slist => bundlesmatching( "$(this.namespace):V_.*" );

  methods:

    (RHEL_7_STIG:enabled.RHEL_7_STIG:chaos_enabled)::
      "RHEL_7_STIG:chaos"
        inherit => "true";

    RHEL_7_STIG:enabled::
      "$(bundles)"
        inherit => "true";

  reports:
    RHEL_7_STIG:enabled.(RHEL_7_STIG:DEBUG|RHEL_7_STIG:DEBUG_run)::
      "Found bundle: $(bundles)";
}
