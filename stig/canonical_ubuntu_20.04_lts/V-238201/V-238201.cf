body file control
{
        namespace => "Canonical_Ubuntu_20_04_LTS_STIG";
}
bundle agent V_238201
# https://www.stigviewer.com/stig/canonical_ubuntu_20.04_lts/2022-06-06/finding/V-238201
{
  meta:
    Canonical_Ubuntu_20_04_LTS_STIG:supported_ubuntu::
      "tags" slist => { "inventory_$(this.namespace)_$(this.bundle)" };

  vars:

    Canonical_Ubuntu_20_04_LTS_STIG:supported_ubuntu::

      # If "use_mappers" is not found or the list does not contain "pwent" this is a finding.

      "check_file" string => "/etc/pam_pkcs11/pam_pkcs11.conf";

      "lines_with_use_mappers"
        slist => grep( ".*use_mappers.*", readstringlist( $(check_file), "", "\n", inf, inf ) ),
        if => fileexists( $(check_file) );

      # It's a finding if the configuration file doesn't exist
      "finding"
        string => "V-238201",
        meta => { "inventory",
                  "attribute_name=Canonical Ubuntu 20.04 LTS STIG V1 findings" },
        if => not( fileexists( $(check_file) ));

      # It's a finding if there are no lines with use_mapper
      "finding"
        string => "V-238201",
        meta => { "inventory",
                  "attribute_name=Canonical Ubuntu 20.04 LTS STIG V1 findings" },
        if => or( not( isvariable( "lines_with_use_mappers" ) ),
                  not( isgreaterthan( length( "lines_with_use_mappers" ), 0 )));

      # It's a finding if pwent is not found in the list of mappers to use
      "finding"
        string => "V-238201",
        meta => { "inventory",
                  "attribute_name=Canonical Ubuntu 20.04 LTS STIG V1 findings" },
        if => not( reglist( lines_with_use_mappers,
                            "^\s*use_mappers\s*=.*[,\s]pwent[\W].*"));
}
