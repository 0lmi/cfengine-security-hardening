body file control
{
        namespace => "Canonical_Ubuntu_20_04_LTS_STIG";
}
bundle agent V_238215
# https://www.stigviewer.com/stig/canonical_ubuntu_20.04_lts/2022-06-06/finding/V-238215
{
  meta:
    Canonical_Ubuntu_20_04_LTS_STIG:supported_ubuntu::
      "tags" slist => { "inventory_$(this.namespace)_$(this.bundle)" };

  classes:
    Canonical_Ubuntu_20_04_LTS_STIG:supported_ubuntu::

      "sshd_service_not_active"
        expression => not( returnszero( "$(paths.systemctl) --quiet is-active sshd.service", noshell) );

      "sshd_service_not_loaded"
        expression => not( strcmp( execresult( "$(paths.systemctl) show -pLoadState sshd.service", noshell),
                                   "LoadState=loaded"));

      "not_one_sshd_server_package_installed"
        not => strcmp( length( packagesmatching( "openssh-server", ".*", ".*", ".*" ) ), 1);

  vars:
    Canonical_Ubuntu_20_04_LTS_STIG:supported_ubuntu::

      # If the "openssh" server package is not installed, this is a finding.

      "finding"
        string => "V-238215",
        meta => { "inventory",
                  "attribute_name=Canonical Ubuntu 20.04 LTS STIG V1 findings" },
        if => "not_one_sshd_server_package_installed";

      # If "sshd.service" is not active or loaded, this is a finding.

      "finding"
        string => "V-238215",
        meta => { "inventory",
                  "attribute_name=Canonical Ubuntu 20.04 LTS STIG V1 findings" },
        if => or( "sshd_service_not_active", "sshd_service_not_loaded" );
}
